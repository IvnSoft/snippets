#!/usr/bin/env perl
use strict;
use warnings;
use Time::Piece;

my $last_month = gmtime() - 86400 * 30;
my @git_argv   = grep { !/^--author=all/ } @ARGV;

push @git_argv, '--pretty=format:%C(cyan)%cd%Creset %C(green)%ad%Creset %C(yellow)%h%Creset %s';
push @git_argv, '--date=short' unless grep {/^--date/} @ARGV;
push @git_argv, '--color' unless grep {/^--no-color/} @ARGV;
push @git_argv, sprintf '--author=%s', git_config('user.email') unless grep {/^--author/} @ARGV;
push @git_argv, sprintf '--since=%s-%02s-01', $last_month->year, $last_month->mon unless grep {/^--since/} @ARGV;

print "$_\n" for sort { $a cmp $b } uniq(git(log => '--reverse' => @git_argv), git(reflog => @git_argv));

sub git_config {
  my $key = shift;
  my $val = qx{git config --get $key};
  chomp $val;
  return $val;
}

sub git {
  warn "\$ git @_\n" if $ENV{DEBUG};
  open my $GIT, '-|', git => @_ or die "git @_: $!";
  my @out;
  s!\s+$!! && push @out, $_ while <$GIT>;
  return @out;
}

sub uniq {
  my %uniq;
  return grep {
    my @l = split /\s/, $_, 4;
    splice @l, 2, 1; # remove the hash
    !$uniq{"@l"}++;
  } @_;
}

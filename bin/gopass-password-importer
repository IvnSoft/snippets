#!/usr/bin/env perl
use feature 'say';
use strict;
use warnings;

# $ cat sample-passwords.csv
# name,url,username,password
# whatever.com,https://whatever.com/login,superwoman,s3cret1
# other.com,https://other.com/login,superwoman,s3cret2


if ($ENV{GOPASS_EDIT_PASSWORD}) {
  open my $TMP, '>', $ARGV[0] or die "Write $ARGV[0]: $!";
  say {$TMP} $ENV{GOPASS_EDIT_PASSWORD};
  say {$TMP} "---";
  say {$TMP} "comment: $ENV{GOPASS_EDIT_NAME}" if $ENV{GOPASS_EDIT_NAME};
  say {$TMP} "url: $ENV{GOPASS_EDIT_URL}" if $ENV{GOPASS_EDIT_URL};
  say {$TMP} "username: $ENV{GOPASS_EDIT_USERNAME}";
  close $TMP;
  exit;
}

$ENV{EDITOR} = $0;
$ENV{GOPASS_PASSWORD_ENTRY} = '/no/such/file';

my $csv_file = shift @ARGV || die "Usage: $0 <password-file.csv>\n";
my @header;

open my $CSV, '<', $csv_file or die "Read password file $csv_file: $!\n";
while (<$CSV>) {
  chomp;

  if (@header) {
    my $i = 0;
    my %row = map { ($header[$i++] => $_) } split /,/, $_, int @header;
    next unless $row{name} and $row{password} and $row{username};
    my $category = $row{name} =~ m!^\d+\.! ? 'ip' : 'website';
    $ENV{uc "GOPASS_EDIT_$_"} = $row{$_} for keys %row;
    my $name = $row{name};
    $name =~ s!^www\.!!;
    system qw(gopass edit -c) => "jhthorsen/$category/$name/$row{username}";
  }
  else {
    @header = split /,/, $_;
  }
}

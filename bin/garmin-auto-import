#!/usr/bin/perl

=head1 NAME

garmin-auto-import - Import Garmin data when connected

=head1 DESCRIPTION

This script is supposed to be started when you log in to Gnome. It will poll
the USB device each second to see if a Garmin device got connected. Once
connected, it will automatically import the files to a local folder.

=head1 SYNOPSIS

    garmin-auto-import;
    garmin-auto-import \
      --vendor 091e \
      --product 0003 \
      --output-dir /home/USERNAME/Documents/garmin \
      ;

=head1 REQUIREMENTS

Running the script should inform you of your missing dependencies.

=over 4

=item garmin-sync

This script should exist in your PATH. garmin-sync can be downloaded from
L<https://launchpad.net/garmin-sync>. Example installation:

    $ cd /usr/local/bin;
    $ sudo tar xfv ~/Downloads/garmin-sync-0.3.tar.gz;

=item Perl modules

This script require L<Device::USB>, L<Getop::Long::Descriptive> and
L<IPC::Run3>. All modules are part of the ubuntu/debian package system
and can be installed with:

    $ sudo apt-get install \
        getopt-long-descriptive \
        libdevice-usb-perl \
        libipc-run3-perl \
        ;

=item Zenity

Zenity is already installed, if you're running Ubuntu. If not, you can install
it with the command below:

   $ sudo apt-get install zenity;

=item Nautilus

Nautilus is the default file manager in Ubuntu. This dependency is optional,
and not required to import the files.

   $ sudo apt-get install nautilus;

=back

=cut

use strict;
use warnings;
use constant DEBUG => $ENV{'GARMIN_DEBUG'} ? 1 : 0;
use LWP::UserAgent;

sub error;
sub question;
sub notify;

my $garmin_sync_app = require_exe_or_die('garmin-sync');
my $zenity_app = require_exe_or_die('zenity');
my $nautilus_app = require_exe_or_die('nautilus');
my $config_file = $ENV{'HOME'} .'/.garmin-auto-import';

require_or_die('Device::USB');
require_or_die('Getopt::Long::Descriptive');
require_or_die('IPC::Run3');

my($opt, $usage) = Getopt::Long::Descriptive::describe_options(
                       'garmin-auto-import <some-arg>',
                       [],
                       [ 'vendor=s', "The vendor USB ID", { default => '091e' } ],
                       [ 'product=s', "The product USB ID", { default => '0003' } ],
                       [ 'output-dir=s', "Output directory for .tcx files", { default => join '/', $ENV{'HOME'}, 'Documents', 'garmin' } ],
                       [],
                       [ 'help', "print usage message and exit" ],
                   );

if($opt->help) {
    sdie('%s', $usage->text);
}

my $usb = Device::USB->new;
my @usb_id = map { hex $_ } @$opt{qw/ vendor product /};
my $connected = 0;

unless(-d $opt->output_dir) {
    mkdir $opt->output_dir or sdie('Failed to create %s: %s', $opt->output_dir, $!);
}

while(1) {
    if(!$usb->find_device(@usb_id)) {
        $connected = 0;
    }
    elsif(!$connected) {
        $connected = time;
        chdir $opt->output_dir or sdie('Failed to change directory to %s: %s', $opt->output_dir, $!);
        symlink '.', 'exports' or warn "Failed to create symlink ./exports: $!";

        warn "Device detected..." if DEBUG;
        warn $opt->output_dir if DEBUG;

        if(question "You have connected a Garmin device.\n\nDo you want to import data?") {
            if(system $garmin_sync_app) {
                error "Could not import data from Garmin device!";
            }
            else {
                sync_success();
            }
        }

        unlink 'exports' or warn "Failed to remove symlink ./exports: $!";
    }
    sleep 1;
}

exit 0; # should never come to this...

sub upload {
    my $ua = LWP::UserAgent->new;
    my $garmin_signin_url = 'http://connect.garmin.com/signin';
    my $garmin_upload_url = 'http://connect.garmin.com/upload-service-1.0/json/upload';
    my $n_uploaded = 0;
    my($username, $password, $res);

    $ua->cookie_jar({});
    push @{ $ua->requests_redirectable }, 'POST';

    unless(-r $config_file) {
        error 'Cannot upload without %s', $config_file;
        return;
    }

    warn "Trying to log in..." if DEBUG;

    $res = $ua->get($garmin_signin_url); # get session id
    $res = $ua->post($garmin_signin_url, # log in
               do $config_file,
               'Content-Type' => 'application/x-www-form-urlencoded',
           );

    warn $res->status_line if DEBUG;

    if($res->is_success) {
        notify 1200, 'Logged in to connect.garmin.com. Starting to upload files...';
    }
    else {
        error 'Could not log in %s', $res->status_line;
        return;
    }

    opendir my $DH, $opt->output_dir or die $!;
    for my $name (readdir $DH) {
        my $file = join '/', $opt->output_dir, $name;;
        next unless($name =~ /\.tcx/);
        next unless($connected <= (stat $file)[9]);

        $res = $ua->post($garmin_upload_url,
            [ data => [$file] ],
            'Content_Type' => 'form-data',
        );

        warn "$file: ", $res->status_line if DEBUG;

        unless($res->is_success) {
            notify 'Could not upload %s: %s', $file, $res->status_line;
            next;
        }

        $n_uploaded++;
    }

    notify 1200, 'Uploaded %s files to connect.garmin.com', $n_uploaded;
}

sub sync_success {
    my @zenity = (
                    'zenity',
                    '--list',
                    '--hide-header',
                    '--multiple',
                    '--column=Selection',
                    "--separator=\n",
                    "--text=Garmin import was successful!\n\nPlease select actions from the list below:",
                 );

    my %options = (
        'Open http://connect.garmin.com/transfer/upload' => 'gnome-open http://connect.garmin.com/transfer/upload',
        'Open ' .$opt->output_dir, 'nautilus ' .$opt->output_dir,
        'Upload imported files', => \&upload,
    );

    IPC::Run3::run3(\@zenity,
        \join("\n", keys %options), # stdin
        \my @selections,            # stdout
        \undef,                     # stderr
    );

    for(map { chomp; $_ } @selections) {
        my $action = $options{$_};
        warn $action if DEBUG;
        unless(fork) {
            if(ref $action eq 'CODE') {
                $action->();
                exit;
            }
            else {
                exec $action;
            }
        }
    }
}

sub question {
    my $format = shift;
    system(zenity => '--question', sprintf "--text=$format", @_) ? 0 : 1;
}

sub error {
    my $format = shift;
    system(zenity => '--error' => sprintf "--text=$format", @_) ? 0 : 1
}

sub notify {
    my $timeout = shift;
    my $format = shift;
    system 'notify-send',
        -t => $timeout,
        -i => 'notification-network-wireless-full',
        sprintf($format, @_),
        ;
}

sub sdie {
    die sprintf shift(@_) ."\n", @_;
}

sub require_exe_or_die {
    my $exe = shift;
    return $exe if(grep { -x "$_/$exe" } split /:/, $ENV{'PATH'});
    die <<"MISSING";
    Required binaries:

    * garmin-sync
      Can be downloaded from://launchpad.net/garmin-sync 

    * nautilus
    \$ sudo apt-get install nautilus

    * zenity
    \$ sudo apt-get install zenity

    =================================================================
    Required binary missing: $exe
    =================================================================

MISSING
}

sub require_or_die {
    my $module = shift;
    return $module if(eval "require $module; 1");
    die <<"MISSING";

    Running ubuntu/debian? Install the module using the command below:

    \$ sudo apt-get install \
        getopt-long-descriptive \
        libdevice-usb-perl \
        libipc-run3-perl \
        ;

    =================================================================
    Require module missing: $module
    =================================================================

MISSING
}
